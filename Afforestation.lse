// Afforestation for REMAINS
LSEVENT: Afforestation

DEFINITIONS
	GLOBAL CONSTANT: DaysPerYr, HaPerCell
	GLOBAL VARIABLE: TimeStep
	LAYER: LCM, PrevLCM, TSChg, TransType
	GLOBAL VARIABLE: RewildingTh, RateAfforestOak, RateAfforestPine
	GLOBAL VARIABLE: AfforestOakRadius, AfforestPineRadius
	GLOBAL CONSTANT: Shrub, Oak, Pine, Rocky, AfforOak, AfforPine, Encro
	EVENT VARIABLE: TargetAfforestOak, TargetAfforestPine
	LOCAL: ShrubForest, AreaAfforOak, AreaAfforPine
	LOCAL: nneigh_oak, nneigh_pine, noak,  npine
	OUTPUT VARIABLE: NatSuccFile = Afforest.txt 
//	OUTPUT VARIABLE: TestFile = TestNatSucc.txt
ENDDEF


INITIALSTATE	
   INITIALSTATE = 1
ENDIS


RETURNTIME
	// Event scheduled once at the end of the year (time units are in days, TimeStep is in years)
	RETURNTIME = IF Time EQ 0 THEN 364 ELSE TimeStep * DaysPerYr

	// Count available shrublands that can become oak/pine (n.cells)
	ShrubForest =  0
	OVER REGION WHOLE MAP
	DECISION (LCM EQ Shrub) AND (TSChg > RewildingTh)
		ShrubForest  = ShrubForest + 1		
	ENDFN	

	// According to the annual rates, determine the target for each transition
	TargetAfforestOak = ROUND(ShrubForest*RateAfforestOak/100) 	// n.cells
	TargetAfforestPine = ROUND(ShrubForest*RateAfforestPine/100) 	// n.cells

	// To track the area changed
	AreaAfforOak = 0
	AreaAfforPine = 0

ENDRT


EVENTLOCATION
	STATIC REGION WHOLE MAP
	DECISION ((LCM EQ Shrub) AND (TSChg > RewildingTh))
ENDEL


NUMCLUSTERS 
	NUMCLUSTERS = (TargetAfforestOak+TargetAfforestPine)*3
ENDNC


PROBINIT
	PROBINIT = ROUND(UNIFORM(1,100))
ENDPI


TRANSITIONS

	TRANSITIONS = (LCM EQ Shrub) AND (TSChg > RewildingTh) AND (TargetAfforestOak+TargetAfforestPine>0)

	// Count oaks within a circular neighborhood and the size of this neighborhood
	nneigh_oak = 0
	noak = 0
	OVER REGION CENTRED (1, AfforestOakRadius)
	DECISION (LCM >0)
		nneigh_oak = nneigh_oak + 1
	END
	OVER REGION CENTRED (1, AfforestOakRadius)
	DECISION (LCM EQ Oak)  // current land-cover is Oak, that is, it hasn't been burnt, otherwise it will be Shurb, Rocky or ShrubForest
		noak = noak + 1 
	END 

	// If succession is happening, then:
	p = UNIFORM(0.001,1)
	IF ((p <= (noak/nneigh_oak)^(1/5)) AND (TargetAfforestOak>0))
		LCM = Oak
		TransType = AfforOak
		TSChg = 0
		TargetAfforestOak = TargetAfforestOak - 1
		AreaAfforOak = AreaAfforOak + HaPerCell
	ENDFN

	// Count pines in a circular neighborhood 
	nneigh_pine = 0
	npine = 0
	OVER REGION CENTRED (1, AfforestPineRadius)
	DECISION (LCM >0)
		nneigh_pine = nneigh_pine + 1
	END
	OVER REGION CENTRED (1, AfforestPineRadius)
	DECISION (LCM EQ Pine)  // current land-cover is Pine, that is, it hasn't been burnt, otherwise it will be Shurb, Rocky or ShrubForest
		npine = npine + 1 
	END 

	// If succession is happening, then:
	p = UNIFORM(0.001,1)
	IF ((p <= (npine/nneigh_pine)^(1/5)) AND (TargetAfforestPine>0) AND (TSChg>0))
		LCM = Pine
		TransType = AfforPine
		TSChg = 0
		TargetAfforestPine = TargetAfforestPine - 1
		AreaAfforPine = AreaAfforPine + HaPerCell
	ENDFN


		// Check
/*	IF TSChg EQ 0
	OUTPUT(TestFile)
		Run
		Year: FLOOR(Time/365.25) + 1
		TransType
		TSChg
		Location	
//		AfforestRadius	
//		EncroachRadius
		noak
		npine
		nshrub
		nneigh		
		p
		th_oak: (noak/nneigh)^(1/5)
		th_pine: (npine/nneigh)^(1/5)
		th_shrub: (nshrub/nneigh)^(1/5)
		LCM
		TargetAfforestOak
		TargetAfforestPine
		TargetEncroach
	ENDFN
	ENDFN */

ENDTR


ENDEVENT

	// Change from Shrub to Oak/Pine and from Rock to Shrub
	OUTPUT(NatSuccFile)
		Run
		Year: FLOOR(Time/365.25) + 1
		PotentialShrub:	ShrubForest*HaPerCell
		//RateAfforestOak
		//RateAfforestPine
		TargetAfforestOak: (ShrubForest*RateAfforestOak/100)*HaPerCell
		TargetAfforestPine: (ShrubForest*RateAfforestPine/100)*HaPerCell
		AreaAfforOak 
		AreaAfforPine 
	ENDFN

	ENDEVENT=TRUE
ENDEE


